// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

/**
 * @title MyNFT
 * @dev 一个完整的NFT合约实现，支持铸造、元数据管理和供应量控制
 * @notice 使用OpenZeppelin库实现标准ERC721功能
 */
contract MyNFT is ERC721, ERC721URIStorage, Ownable {
    uint256 private _tokenIdCounter; // 用于生成唯一的tokenId
    uint256 public constant MAX_SUPPLY = 10000; // 最大供应量
    uint256 public mintPrice = 0.01 ether; // 铸造价格

    /**
     * @dev NFT铸造事件
     * @param minter 铸造者地址
     * @param tokenId 新创建的Token ID
     * @param uri 元数据URI
     */
    event NFTMinted(
        address indexed minter,
        uint256 indexed tokenId,
        string uri
    );

    constructor() ERC721("MyNFT", "MNFT") Ownable(msg.sender) {}

    function mint(string memory uri) public payable returns (uint256) {
        // 检查供应量限制
        require(_tokenIdCounter < MAX_SUPPLY, "Max supply reached");
        // 检查支付金额
        require(msg.value >= mintPrice, "Insufficient payment");

        _tokenIdCounter++;
        // 递增计数器
        _tokenIdCounter++;
        uint256 newTokenId = _tokenIdCounter;
        // 安全铸造NFT
        // _safeMint是OpenZeppelin提供的内部函数，它会创建NFT并转移到to地址，同时进行必要的安全检查
        _safeMint(msg.sender, newTokenId);
        // 设置元数据URI
        //_setTokenURI设置元数据URI，这样外部应用就可以通过tokenURI函数获取NFT的展示信息
        _setTokenURI(newTokenId, uri);
        // 触发事件
        emit NFTMinted(msg.sender, newTokenId, uri);
        return newTokenId;
    }

    /**
     * @dev 重写tokenURI函数
     * @param tokenId Token ID
     * @return 元数据URI
     * @notice 需要重写以解决多重继承的冲突
     * 我们重写了tokenURI函数。这是因为我们同时继承了ERC721和ERC721URIStorage，
     * 两个合约都实现了这个函数，所以需要明确指定使用哪个实现。这里我们使用super.tokenURI，会按照继承顺序调用正确的实现。
     */
    function tokenURI(
        uint256 tokenId
    ) public view override(ERC721, ERC721URIStorage) returns (string memory) {
        return super.tokenURI(tokenId);
    }

    /**
     * @dev 检查接口支持
     * @param interfaceId 接口ID
     * @return 是否支持该接口
     * @notice 实现ERC165标准，支持接口查询
     * 这个函数用于检查合约是否支持某个接口。这对于ERC165标准兼容性很重要，也是ERC2981版税标准的基础。
     */
    function supportsInterface(
        bytes4 interfaceId
    ) public view override(ERC721, ERC721URIStorage) returns (bool) {
        return super.supportsInterface(interfaceId);
    }

    /**
     * @dev 查询总供应量
     * @return 已铸造的NFT数量
     */
    function totalSupply() public view returns (uint256) {
        return _tokenIdCounter;
    }

    /**
     * @dev 提取铸造费用
     * @notice 只有合约所有者可以调用
     */
    function withdraw() public onlyOwner {
        uint256 balance = address(this).balance;
        require(balance > 0, "No balance to withdraw");
        payable(owner()).transfer(balance);
    }

    /**
     * @dev 设置铸造价格
     * @param _newPrice 新的铸造价格（wei）
     * @notice 只有合约所有者可以调用
     */
    function setMinPrice(uint256 _newPrice) public onlyOwner {
        mintPrice = _newPrice;
    }
}
